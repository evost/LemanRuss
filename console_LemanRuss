#define pinX          A0
#define pinY          A1
#define keyA          2
#define keyB          3
#define keyC          4
#define keyD          5
#define keyE          6
#define keyF          7
#define keyK          8
#define led           13
#define bounce        32
#define breaking_rc   ' '
#define forward_rc    'w'
#define back_rc       's'
#define right_rc      'd'
#define left_rc       'a'
#define forward2_rc   'i'
#define back2_rc      'k'
#define right2_rc     'l'
#define left2_rc      'j'
#define changmod_rc   '<'
#define changsrv_rc   '>'
#define servos_to_def '/'
int calibrateX = 512;
int calibrateY = 512;
int x = calibrateX;
int y = calibrateY;
bool manual = true;
bool servo = false;
bool fast = false;
bool is_break = true;
void setup() {
  pinMode(led, OUTPUT);
  for (byte i = keyA; i <= keyK; i++)
    pinMode(i, INPUT);
  calibrateX = analogRead(pinX);
  calibrateY = analogRead(pinY);
  Serial.begin(9600);
  while (!Serial);
  while (true) {
    Serial.write(forward_rc);
    digitalWrite(led, HIGH);
    delay(256);
    digitalWrite(led, LOW);
    delay(256);
    if (Serial.available()) {
      while (Serial.read() != back_rc);
      digitalWrite(led, HIGH);
      Serial.write(breaking_rc);
      break;
    }
  }
}
void loop() {
  if (digitalRead(keyF) == LOW) {
    if (servo)
      fast = !fast;
    else {
      Serial.write(changmod_rc);
      manual = !manual;
      if (manual)
        digitalWrite(led, HIGH);
      else
        digitalWrite(led, LOW);
    }
    while (digitalRead(keyF) == LOW);
  }
  if (digitalRead(keyE) == LOW) {
    Serial.write(changsrv_rc);
    servo = !servo;
    // fast = false;
    while (digitalRead(keyE) == LOW);
  }
  if (digitalRead(keyK) == LOW) {
    Serial.write(servos_to_def);
    while (digitalRead(keyK) == LOW);
  }
  y = analogRead(pinY);
  if (servo) {
    if (y < calibrateY - bounce)
      Serial.write(back2_rc);
    else if (y > calibrateY + bounce)
      Serial.write(forward2_rc);
  } else if (manual) {
    if (digitalRead(keyA) != LOW && digitalRead(keyB) != LOW &&
        digitalRead(keyC) != LOW && digitalRead(keyD) != LOW) {
      if (!is_break) {
        Serial.write(breaking_rc);
        is_break = true;
      }
    } else {
      Serial.write(128 + y / 8);
      is_break = false;
    }
  }
  x = analogRead(pinX);
  if (x < calibrateX - bounce)
    Serial.write(left2_rc);
  else if (x > calibrateX + bounce)
    Serial.write(right2_rc);
  if (digitalRead(keyA) == LOW)
    Serial.write(forward_rc);
  else if (digitalRead(keyC) == LOW)
    Serial.write(back_rc);
  if (digitalRead(keyB) == LOW)
    Serial.write(right_rc);
  else if (digitalRead(keyD) == LOW)
    Serial.write(left_rc);
  if (fast && servo)
    delay(8);
  else
    delay(32);
}